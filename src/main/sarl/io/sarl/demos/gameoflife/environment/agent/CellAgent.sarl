/** 
 * 
 */
package io.sarl.demos.gameoflife.environment.^agent

import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import io.sarl.demos.gameoflife.environment.^agent.events.CellAgentReady
import io.sarl.demos.gameoflife.environment.^agent.events.CellAgentUpdated
import io.sarl.demos.gameoflife.environment.^agent.events.CellOff
import io.sarl.demos.gameoflife.environment.^agent.events.CellOn
import io.sarl.demos.gameoflife.environment.^agent.events.GameIsDead

/** 
 * @author Jérôme BOULMIER
 * 
 */
agent CellAgent {
	uses DefaultContextInteractions, Lifecycle;
	
	on Initialize {
		emit(new CellAgentReady)
	}

	on Perception {
		if (occurrence.alive == true) {
			if (occurrence.neighbors.size() != 2 && occurrence.neighbors.size() != 3) {
				emit(new CellOff(ID, occurrence.position))
			}
		} else {
			if (occurrence.neighbors.size() == 3) {
				emit(new CellOn(ID, occurrence.position))
			}
		}
		
		emit(new CellAgentUpdated)
	}
	
	on GameIsDead {
		killMe
	}
}
