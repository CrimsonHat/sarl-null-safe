/**
 *
 */
package io.sarl.demos.gameoflife

import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import io.sarl.demos.gameoflife.environment.^agent.Controller
import io.sarl.demos.gameoflife.environment.^agent.Environment
import io.sarl.demos.gameoflife.environment.^agent.events.AgentReadyForGame
import io.sarl.demos.gameoflife.gui.GUI
import io.sarl.lang.core.Address
import java.util.UUID
import io.sarl.core.Logging
import javafx.application.Platform
import java.util.concurrent.atomic.AtomicInteger

/**
 * @author Maxime PINARD
 *
 */
agent Boot {
	uses DefaultContextInteractions, Lifecycle, Logging;

	var controller : Controller
	val agentCounter = new AtomicInteger
	var numberOfAgent : int
	var gui : GUI
	val width = 30
	val height = 30

	on Initialize {
		val environmentUUID = UUID.randomUUID

		gui = GUI.getGUI();
		controller = new Controller(defaultSpace, new Address(defaultSpace.spaceID, getID()),
			environmentUUID);
		gui.addGUIListener(controller);

		numberOfAgent = width * height + 1;
		info("There is " + numberOfAgent + " agents.")
		spawnInContextWithID(Environment, environmentUUID, defaultContext, width, height, gui);
	}

	on AgentReadyForGame {
		var tmp = agentCounter.incrementAndGet
		
		info("Entity loaded: " + tmp)
		//info("Updated: " + agentCounter);

		if (tmp == numberOfAgent) {
			Platform.runLater([gui.launchGUI(width, height)])
			controller.play
			killMe;
		}
	}
}
