package io.sarl.demos.fireworks.agents

import io.sarl.core.AgentTask
import io.sarl.core.Behaviors
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import io.sarl.core.Logging
import io.sarl.core.Schedules
import io.sarl.demos.fireworks.Configuration
import io.sarl.demos.fireworks.environment.Positions
import io.sarl.demos.fireworks.events.FireReady
import io.sarl.demos.fireworks.events.Freeze
import io.sarl.demos.fireworks.events.UpdateFirePosition
import io.sarl.javafx.AppExit
import java.util.ArrayList
import java.util.List
import java.util.UUID
import java.util.Vector

/* Fire agent moving with a nice trajectory.
 */
agent Fire {

	uses Lifecycle, Logging, Behaviors, DefaultContextInteractions, Schedules

	val id = UUID::randomUUID

	var x : List<Double>
	var y : List<Double>
	var lifetime = 300
	var frozen = false
	var destroyed = false
	var exited = false
	var grid : Positions
	var xf : double
	var yf : double
	var vx : double
	var vy : double
	var parentID : UUID
	var move : AgentTask
	var parentAgent : UUID
	var gravity: double

	/** Freeze itself.
	 */
	on Freeze {
		this.frozen = occurrence.value
		if (frozen)
			move.cancel
		else
			wake(new UpdateFirePosition)
	}

	/** Initialization step.
	 */
	on Initialize {
		loggingName = "Fire-" + this.ID
		x = new ArrayList
		y = new ArrayList
		if (occurrence.parameters.size == 5) {
			x += occurrence.parameters.get(0) as Double
			y += occurrence.parameters.get(1) as Double
			grid = occurrence.parameters.get(2) as Positions
			parentID = occurrence.parameters.get(3) as UUID
			gravity = occurrence.parameters.get(4) as Double
		} else {
			info("Error in fire Initialize : bad parameter number")
		}
		parentAgent = occurrence.spawner

		xf = Math::random * 10 - Math::random * 5
		yf = Math::random * 10 + 1
		
		vx = xf * 20
		vy = yf * 30
		
	}

	/** Trigger fixed delay for waking UpdateFirePosition.
	 */
	on FireReady {
		move = atFixedDelay(Configuration::FIRE_LIFECYCLE_SCHEDULING_RATE) [
			wake(new UpdateFirePosition)
		]
	}

	/** Update position every time it's triggered by itself.
	 */
	on UpdateFirePosition [occurrence.isFromMe && !frozen && !destroyed] {
		var newx : double
		var newy : double
		
		vy = vy - gravity * Configuration::FIRE_LIFECYCLE_SCHEDULING_RATE / 10.0
		
		newx = x.last + vx * Configuration::FIRE_LIFECYCLE_SCHEDULING_RATE / 1000.0
		newy = y.last + vy * Configuration::FIRE_LIFECYCLE_SCHEDULING_RATE / 1000.0

		x += newx
		y += newy

		if (grid !== null) {
			var list = new ArrayList<Vector<Double>>
			x.forEach [pos |
				{
					var nvect = new Vector<Double>(2)
					nvect += pos
					list += nvect
				}
			]
			y.forEach [pos, i |
				{
					var nvect = list.get(i)
					nvect += pos
					list.set(i, nvect)
				}
			]
			grid.setFirePosition(id, parentID, list)
		}

		lifetime = lifetime - 10

		if (lifetime <= 0) {
			grid.removeFirePosition(id)
			cleanBeforeExit
			in(1.seconds) [killMe]
		}
	}

	/** Clean every task.
	 */
	def cleanBeforeExit() {
		move.cancel
		exited = true
		destroyed = true
	}

	/** Kill itself when Exit signal invoked from parent.
	 */
	on AppExit [!exited && isFrom(this.parentID)] {
		frozen = true
		cleanBeforeExit
		emit(new AppExit) [it.UUID == this.parentAgent]
		killMe
	}

}
