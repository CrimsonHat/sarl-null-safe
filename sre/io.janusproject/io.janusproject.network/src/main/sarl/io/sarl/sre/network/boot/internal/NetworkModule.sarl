/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2020 the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.network.boot.internal

import com.google.inject.AbstractModule
import com.google.inject.Injector
import com.google.inject.Module
import com.google.inject.Provides
import com.hazelcast.config.Config
import com.hazelcast.core.Hazelcast
import com.hazelcast.core.HazelcastInstance
import com.hazelcast.logging.LogEvent
import com.hazelcast.logging.LogListener
import io.bootique.BQModule
import io.bootique.BQModuleProvider
import io.sarl.api.bootiquebase.config.LogConfig
import io.sarl.sre.boot.configs.SreConfig
import io.sarl.sre.boot.internal.services.ContextServiceModule
import io.sarl.sre.network.boot.configs.JoinMethod
import io.sarl.sre.network.boot.configs.SreNetworkConfig
import io.sarl.sre.network.services.HazelcastContextService
import io.sarl.sre.network.services.HazelcastEventTransportService
import io.sarl.sre.network.services.HazelcastSpaceRepository
import io.sarl.sre.services.context.ContextService
import io.sarl.sre.services.context.LocalSpaceRepository
import io.sarl.sre.services.context.MemoryBasedContextService
import io.sarl.sre.services.context.SpaceRepository
import io.sarl.sre.services.logging.LoggingService
import io.sarl.sre.spaces.EventTransportService
import io.sarl.sre.spaces.LocalEventTransportService
import java.lang.reflect.Type
import java.text.MessageFormat
import java.util.Collection
import java.util.Collections
import java.util.Map
import java.util.logging.Level
import java.util.logging.Logger
import javax.inject.Inject
import javax.inject.Singleton

import static io.sarl.sre.network.boot.configs.SreNetworkConfig.PREFIX

/** 
 * The module for injected the network features of Janus.
 * 
 * @author $Author: ngaud$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.12
 */
class NetworkModule extends AbstractModule {

	/** 
	 * Regexp of a quarter of an IP adress
	 */
	val zeroTo255 : String = "([01]?[0-9]{1,2}|2[0-4][0-9]|25[0-5])";
	
	/**
	 * Regexp of a full IP address
	 */
	val IP_REGEXP : String = zeroTo255 + "\\." + zeroTo255 + "\\." + zeroTo255 + "\\." + zeroTo255;	

	protected override configure {
	}

	@Provides
	def provideSpaceRepository(injector : Injector, netConfig : SreNetworkConfig) : SpaceRepository {
		var instanceType : Class<? extends SpaceRepository>
		if (netConfig.enable) {
			instanceType = typeof(HazelcastSpaceRepository)
		} else {
			instanceType = typeof(LocalSpaceRepository)
		}
		return injector.getInstance(instanceType)
	}

	@Provides
	@Singleton
	def provideContextService(injector : Injector, netConfig : SreNetworkConfig) : ContextService {
		var instanceType : Class<? extends ContextService>
		if (netConfig.enable) {
			instanceType = typeof(HazelcastContextService)
		} else {
			instanceType = typeof(MemoryBasedContextService)
		}
		return injector.getInstance(instanceType)
	}

	@Provides
	@Singleton
	def provideEventTransportService(injector : Injector, netConfig : SreNetworkConfig) : EventTransportService {
		var instanceType : Class<? extends EventTransportService>
		if (netConfig.enable) {
			instanceType = typeof(HazelcastEventTransportService)
		} else {
			instanceType = typeof(LocalEventTransportService)
		}
		return injector.getInstance(instanceType)
	}

	@Provides
	@Singleton
	def provideHazelcastInstance(logger : LoggingService, netConfig : SreNetworkConfig, sreConfig : SreConfig, logConfig : LogConfig) : HazelcastInstance {
		//FIXME config a two hazelcast instances on the same node
		//FIXME config the log for hazelcast to trace error at startup		
		val config : Config = new Config()
		config.clusterName = netConfig.getRealClusterName(sreConfig)
		if (netConfig.joinMethod === JoinMethod::MULTICAST) {
			config.networkConfig.join.multicastConfig.enabled = true
			config.networkConfig.join.tcpIpConfig.enabled = false
		} else {
			config.networkConfig.join.multicastConfig.enabled = false
			config.networkConfig.join.tcpIpConfig.enabled = true
			
				var iplist : String = netConfig.getClusterMemberList();
				var ipArray : String[] = iplist.split(",");
				
				for (s: ipArray) {
					if (!s.matches(IP_REGEXP)) {
					//Mandatory to have the following line if you want to be sure that two distinct hazelcast instances running in two distinct jvm on the same node/computer join the same cluster
					// config.networkConfig.join.tcpIpConfig.addMember("127.0.0.1")
					config.networkConfig.join.tcpIpConfig.addMember(s)
					}
				}
			
		}
		
		config.networkConfig.portAutoIncrement = netConfig.portAutoIncrement
		//The following line imposes that at least two hazelcast instance been laucnhed on the same cluster before doing anything
		config.setProperty("hazelcast.initial.min.cluster.size", netConfig.minClusterSize.toString);

		val hzlInstance = Hazelcast.newHazelcastInstance(config)

		hzlInstance.loggingService.addLogListener(logConfig.julLevel, new HazelcastLogListener(logger))
		return hzlInstance
	}

}

/**
 * Log listener for Hazelcast.
 *
 * @author $Author: ngaud$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.12
 */
class HazelcastLogListener implements LogListener {

	/** 
	 * SRE Kernel logger
	 */
	var logger : Logger
	
	@Inject
	new(logger : LoggingService) {
		this.logger = logger.getKernelModuleLogger(Messages::HazelcastLogListener_1)
	}
	
	
	def log(logEvent : LogEvent) {		
		this.logger.log(logEvent.logRecord.level,
			MessageFormat::format(Messages::HazelcastLogListener_0, logEvent.member, logEvent.logRecord.message))
	}
	
}

/**
 * Bootique module provider for the network features of Janus.
 *
 * @author $Author: ngaud$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.12
 */
class NetworkModuleProvider implements BQModuleProvider {

	override module : Module {
		return new NetworkModule
	}

	override moduleBuilder : BQModule.Builder {
		return BQModule::builder(module).overrides(overrides).providerName(name).configs(configs).description(Messages::NetworkModuleProvider_0)
	}
	
	override overrides : Collection<Class<? extends Module>> {
		#[ContextServiceModule]
	}

	override configs : Map<String, Type> {
		return Collections::singletonMap(PREFIX, typeof(SreNetworkConfig))
	}

}
